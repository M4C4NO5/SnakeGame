// Copyright 2012 Mark Armbrust. Permission granted for educational use.

/** Random.jack -- A not so random PRNG. */

class Random {

    static int seed;

    function void setSeed(int newSeed) {
        let seed = newSeed;
        return;
    }

    function int rand() {
        //Return a random number from 0 to 32767 ...
        let seed = seed + 20251;
        if (seed < 0) {
            let seed = seed - 32767 - 1;
        }
        return seed;
    }

    function int randRange(int range) {
        //Return a random number from 0 to range ...
        var int mask;
        var int ret;
        let mask = 1;
        while (mask < range) {
            let mask = mask * 2 + 1;
        }
        let ret = Random.rand() & mask;
        while (ret > range) {
            let ret = Random.rand() & mask;
        }
        return ret;
    }

}